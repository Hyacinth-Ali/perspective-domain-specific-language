grammar ca.mcgill.sel.perspectivedsl.ca.mcgill.sel.PerspectiveDSL with org.eclipse.xtext.common.Terminals

generate perspectiveDSL "http://www.mcgill.ca/sel/perspectivedsl/ca/mcgill/sel/PerspectiveDSL"

PerspectiveModel:
    (perspectives += Perspective)*
;


Perspective:
    'perspective' name = ID '{'

        ('default' default = ID';')?
        
        'display' 'name' displayName = STRING';'

        'languages' '{'
            (languages += Language)*
        '}'

        ('actions' '{'
            (actions += PerspectiveAction)*
        '}')?
        
        ('facade' 'actions' '{'
        	(facadeActions += FacadeAction)*
        '}')?

        ('mappings' '{'
            (mappings += LanguageElementMapping)*
        '}')?

    '}'
;

Language:
    'existing' 'language' name = ID '{'
        "rootPackage" rootPackage = STRING';'
        "packageClassName" packageClassName = ID';'
        "controllerPackage" controllerPackage = STRING';'
        "roleName" roleName = ID';'
        'utilPackage' ':' utilPackage = STRING';' 
        (otherRootPackages += OtherRootPackage)*
    '}'
;

OtherRootPackage:
	"otherRootPackage" otherRootPackage = STRING';'
;

LanguageElementMapping:
    'mapping' name = ID '{'
//        'relationship' ':' relationship = Relationship';'
        'fromCardinality' ':' fromCardinality = Cardinality';'
        'toCardinality' ':' toCardinality = Cardinality';'
        'fromElement' ':' fromElement = ID 'from' fromRoleName = ID 'fromGetElement' fromGetElement = STRING';'
        'toELement' ':' toElement = ID 'from' toRoleName = ID 'toGetElement' toGetElement = STRING';'

        ('nested' 'mappings' '{'
            (nestedMappings += NestedMapping)*
        '}')?
    '}'
;

NestedMapping:
    'nested' 'mapping' name = ID '{'
    	'matchMaker' ':' matchMaker = MatchMaker';'
        'fromElement' ':' 'elementName' fromElementName = STRING 'from' fromRoleName = ID';'
        'toELement' ':' 'elementName' toElementName = STRING 'from' toRoleName = ID';'
    '}'
;


PerspectiveAction:
    perspectiveActionType = PerspectiveActionType langActionType = LanguageActionType 'action' name = ID (end ?= ';')? ('{'
    	'roleName' ':' roleName = ID';'
    	'methodTypeAndParameters' ':' typeParameters = STRING';'
    	'methodCall' ':' methodCall = STRING';'
    	'methodParameters' ':' methodParameter = STRING';'
    	'metaclassName' ':' metaclassName = ID';'
    	'metaclassObject' ':' metaclassObject = STRING';'
    	('secondaryEffects' '{'
    		(createEffects += CreateEffect)*
    		(deleteEffects += DeleteEffect)*
    	'}')?
    '}')?
;

FacadeAction:
    'facadeAction' name = ID '{'
    	'roleName' ':' roleName = ID';'
    	'metaclassName' ':' metaclassName = ID';'
    	'methodTypeAndParameters' ':' typeParameters = STRING';'
    	'facade' 'calls' '{'
    		(facadeCalls += FacadeCall)*
    	'}'
    '}'
;

FacadeCall:
	'metaclassObject' ':' metaclassObject = STRING';'
	(mappings += ParameterMApping)*
	'methodCall' ':' methodCall = STRING';'   
;

ParameterMApping:
	'mapping' mapping = STRING';'
;

CreateEffect:
	'languageElement' ':' languageElement = STRING';'
;

DeleteEffect:
	'element' ':' element = STRING';'
;



//enum Relationship:
//    EQUALITY='equality' | REFINES='refines' | USES='uses' | EXTENDS='extends' | REQUIRES='requires'
//;

enum Cardinality:
    COMPULSORY='1' | OPTIONAL='0..1' | COMPULSORY_MULTIPLE='1..*' | OPTIONAL_MULTIPLE='0..*'
;

enum PerspectiveActionType :
    REEXPOSE='re_expose' | REDEFINED='redefined' | CREATE_MAPPING='create_mapping'
;

enum LanguageActionType :
	CREATE ='create' | DELETE='delete' | UPDATE='update'
;

enum MatchMaker:
    TRUE='true' | FALSE='false' 
;