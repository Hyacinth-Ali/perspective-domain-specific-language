grammar ca.mcgill.sel.perspectivedsl.ca.mcgill.sel.PerspectiveDSL with org.eclipse.xtext.common.Terminals

generate perspectiveDSL "http://www.mcgill.ca/sel/perspectivedsl/ca/mcgill/sel/PerspectiveDSL"

PerspectiveModel:
    (perspectives += Perspective)*
;


Perspective:
    'perspective' '{'
    
    	'name'':' name = STRING';'
        ('default'':' default = ID';')?
        ('saveConcern'':' saveConcern = STRING';')?
        ('saveModel'':' saveModel = STRING';')?
        
        ('model' 'factory' 'facade' 'calls' '{'
    		(modelFacades += FacadeCall)*
    	'}')?

        'languages' '{'
            (languages += Language)*
        '}'
        
        ('mapping' 'ends' '{'
            (mappingEnds += MappingEnd)*
        '}')?

        ('mappings' '{'
            (mappings += LanguageElementMapping)*
        '}')?
        
        ('navigation' 'mappings' '{'
            (navigationMappings += NavigationMapping)*
        '}')?

    '}'
;

Language:
    'existing' 'language' name = ID '{'
        "rootPackage" rootPackage = STRING';'
        "packageClassName" packageClassName = ID';'
        "controllerPackage" controllerPackage = STRING';'
        "roleName" roleName = ID';' 
        (otherRootPackages += OtherRootPackage)*
        (explicitPackages += ExplicitPackage)*
        ('actions' '{'
            (actions += PerspectiveAction)*
        '}')?
        
    '}'
;

LanguageElementMapping:
    'mapping' name = ID '{'
    	('tag' ':' tag = STRING';')?
    	
		fromMappingEnd = FromMappingEnd
		
		toMappingEnd = ToMappingEnd
        
        ('nested' 'mappings' '{'
            (nestedMappings += NestedMapping)*
        '}')?
    '}'
;

MappingEnd:
	FromMappingEnd | FromMappingEnd
;

FromMappingEnd:
	'from' 'mappingEnd' name = ID '{'
		'isRootElement' ':' isRootElement = BooleanType';'
    	'cardinality' ':' cardinality = Cardinality';'
    	'language' 'element' ':' elementName = ID 'from' roleName = ID 'getElement' getElement = STRING';'
	'}'	
;

ToMappingEnd:
	'to' 'mappingEnd' name = ID '{'
		'isRootElement' ':' isRootElement = BooleanType';'
    	'cardinality' ':' cardinality = Cardinality';'
    	'language' 'element' ':' elementName = ID 'from' roleName = ID 'getElement' getElement = STRING';'
	'}'	
;

NestedMapping:
    'nested' 'mapping' name = ID '{'
    	'matchMaker' ':' matchMaker = BooleanType';'
        'fromElement' ':' fromElementName = STRING 'from' fromRoleName = ID';'
        'toElement' ':' toElementName = STRING 'from' toRoleName = ID';'
    '}'
;

PerspectiveAction:
	CreateAction | DeleteAction
;


CreateAction:
    perspectiveActionType = PerspectiveActionType 'create' 'action' name = ID (end ?= ';')? ('{'
    	(rootElement ?= 'rootElement'';')?
    	'roleName' ':' roleName = ID';'
    	'methodTypeAndParameters' ':' typeParameters = STRING';'
    	'methodCall' ':' methodCall = STRING';'
    	'methodParameters' ':' methodParameter = STRING';'
    	'languageElementName' ':' languageElementName = ID';'
    	'languageElement' ':' languageElement = STRING';'
    	
    	// constraints for root model elements
		(constraints += Constraint)*
    	createFacadeAction = CreateFacadeAction
    	('secondaryEffects' '{'
    		('create' 'effects' '{'
    			(createEffects += CreateEffect)*
    		'}')?
    		('delete' 'effects' '{'
    			(deleteEffects += DeleteEffect)*
    		'}')?
    	'}')?
    '}')?
;

DeleteAction:
    perspectiveActionType = PerspectiveActionType 'delete' 'action' name = ID (end ?= ';')? ('{'
    	(rootElement ?= 'rootElement'';')?
    	'roleName' ':' roleName = ID';'
    	'methodTypeAndParameters' ':' typeParameters = STRING';'
    	'methodCall' ':' methodCall = STRING';'
    	'methodParameters' ':' methodParameter = STRING';'
    	'languageElementName' ':' languageElementName = ID';'
    	'languageElement' ':' languageElement = STRING';'
    	
    	// constraints for root model elements
		(constraints += Constraint)*
    	deleteFacadeAction = DeleteFacadeAction
    	('secondaryEffects' '{'
    		('create' 'effects' '{'
    			(createEffects += CreateEffect)*
    		'}')?
    		('delete' 'effects' '{'
    			(deleteEffects += DeleteEffect)*
    		'}')?
    	'}')?
    '}')?
;

CreateFacadeAction:
    'facadeAction' 'create' name = ID '{'
    	'facade' 'calls' '{'
    		(facadeCalls += FacadeCall)*
    	'}'
    '}'
;

DeleteFacadeAction:
	'facadeAction' 'delete' name = ID '{'
		'facade' 'calls' '{'
			(facadeCalls += FacadeCall)+
		'}'
		
	'}'
;

OtherRootPackage:
	"otherRootPackage" otherRootPackage = STRING';'
;

ExplicitPackage:
	"explicitPackage" explicitPackage = STRING';'
;

//MethodCall:
//	'metaclassObject' ':' metaclassObject = STRING';'
//	'methodCall' ':' methodCall = STRING';'
//;

FacadeCall:
	'languageElement' ':' languageElement = STRING';'
	// constraints
	(constraints += Constraint)*
	(mappings += ParameterMapping)*
	'methodCall' ':' methodCall = STRING';'   
;


ParameterMapping:
	'derivedParameter' mapping = STRING';'
;

CreateEffect:
	'languageElement' ':' languageElement = STRING';'
	(mappings += ParameterMapping)*
	'methodCall' ':' methodCall = STRING';'
;

DeleteEffect:
	'element' ':' element = STRING';'
	'languageElement' ':' languageElement = STRING';'
	(mappings += ParameterMapping)*
	'methodCall' ':' methodCall = STRING';'
;

NavigationMapping:
	InterLanguageMapping | IntraLanguageMapping
	
;

InterLanguageMapping:
	'interLanguage' 'mapping' name = ID '{'
		(active ?= 'active')?
		(default ?= 'default')?
		'languageMapping' ':' languageMapping = [LanguageElementMapping]';'
		'interLanguage' 'mappingEnds' '{'
			(interLanguageMappingEnds += InterLanguageMappingEnd)*
		'}' 
	'}'
	
;

IntraLanguageMapping:
	'intraLanguage' 'mapping' name = ID '{'
		(active ?= 'active')?
		(closure ?= 'closure')?
		(reuse ?= 'reuse')?
		(increaseDepth ?= 'increaseDepth')?
		(changeModel ?= 'changeModel')?
		'from' ':' from = ID';'
		'hops' '{'
			(hop += Hop)*
		'}'
	'}'	
;

InterLanguageMappingEnd:
	'mapping' 'end' name = ID '{'
		(origin ?= 'origin')?
		(destination ?= 'destination')?
		// These are two optional references to either from or to mapping end 
		// The perspective designer has to use one of them, because each instance
		// of an InterLanguageMappingEnd references one instance of the MappingEnd metaclass.
		'mappingEnd' ':' fromMappingEnd = [FromMappingEnd]
	'}'
;

Hop:
	'hop' ':' hop = ID';'
;

Constraint:
	'invariant' 'constraint' ':' definition = STRING';'
;



enum Cardinality:
    COMPULSORY = '1' | OPTIONAL='0..1' | COMPULSORY_MULTIPLE='1..*' | OPTIONAL_MULTIPLE='0..*'
;

enum PerspectiveActionType :
    REEXPOSE='re_expose' | REDEFINED='redefined' | CREATE_MAPPING='create_mapping'
;

enum LanguageActionType :
	CREATE = 'create' | DELETE = 'delete' | UPDATE = 'update'
;

enum BooleanType:
    TRUE = 'true' | FALSE = 'false' 
;




