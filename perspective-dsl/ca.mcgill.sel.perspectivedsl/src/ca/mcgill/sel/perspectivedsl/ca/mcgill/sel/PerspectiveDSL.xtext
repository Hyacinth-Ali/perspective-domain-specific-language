grammar ca.mcgill.sel.perspectivedsl.ca.mcgill.sel.PerspectiveDSL with org.eclipse.xtext.common.Terminals

generate perspectiveDSL "http://www.mcgill.ca/sel/perspectivedsl/ca/mcgill/sel/PerspectiveDSL"

PerspectiveModel:
    (perspectives += Perspective)*
;


Perspective:
    'perspective' '{'
    
    	'name'':' name = STRING';'
        // to dictate the model shown by default with the perspective
        ('default'':' default = ID';')?
        
        // customizes the perspective to support persistence for both
        // language models and the perspective. Note that in TouchCORE,
        // the scene should be used as the parameter since the model element mappings are contained
        // in the concern of the models. The perspective containing the model element
        // mappings is only for formal description of the Thesis work.
        // See sample DSL models for further illustration.
        ('savePerspective'':' savePerspective = STRING';')?
        ('saveModel'':' saveModel = STRING';')?
        
        // When a root model is mapped with another regular language element,
        // a facade call is required to call the recursive action of the regular 
        // language element to propagate the changes after creating the model in
        // question. See sample models for further illustration
        ('model' 'factory' 'facade' 'calls' '{'
    		(modelFacades += FacadeCall)*
    	'}')?

		// language registration as well as the perspective actions
		// of each language
        'languages' '{'
            (languages += Language)*
        '}'
        
        // enumerate mapping ends which can be referenced from the language
        // element mapping or inter language mapping end.
        ('mapping' 'ends' '{'
            (mappingEnds += MappingEnd)*
        '}')?

        ('mappings' '{'
            (mappings += LanguageElementMapping)*
        '}')?
        
        ('navigation' 'mappings' '{'
            (navigationMappings += NavigationMapping)*
        '}')?

    '}'
;

Language:
    'existing' 'language' name = ID '{'
        "rootPackage" rootPackage = STRING';'
        "controllerPackage" controllerPackage = STRING';'
        "roleName" roleName = ID';' 
        (otherRootPackages += OtherRootPackage)*
        (explicitPackages += ExplicitPackage)*
        ('actions' '{'
            (actions += PerspectiveAction)*
        '}')?
        
    '}'
;

LanguageElementMapping:
    'mapping' name = ID '{'
    	('tag' ':' tag = STRING';')?
    	
		'fromMappingEnd' ':' fromMappingEnd = [MappingEnd]';'
		'toMappingEnd' ':' toMappingEnd = [MappingEnd]';'
        
        ('nested' 'mappings' '{'
            (nestedMappings += NestedMapping)*
        '}')?
    '}'
;

MappingEnd:
	'mappingEnd' name = ID '{'
		'isRootElement' ':' isRootElement = BooleanType';'
    	'cardinality' ':' cardinality = Cardinality';'
    	'roleName' ':' roleName = ID';'
    	'languageElement' ':' languageElement = STRING';'
	'}'	
;

NestedMapping:
    'nested' 'mapping' name = ID '{'
    	'matchMaker' ':' matchMaker = BooleanType';'
        'fromElement' ':' fromElementName = STRING 'from' fromRoleName = ID';'
        'toElement' ':' toElementName = STRING 'from' toRoleName = ID';'
    '}'
;

PerspectiveAction:
	CreateAction | DeleteAction
;


CreateAction:
    perspectiveActionType = PerspectiveActionType 'create' 'action' name = ID (end ?= ';')? ('{'
    	(rootElement ?= 'rootElement'';')?
    	'roleName' ':' roleName = ID';'
    	'methodTypeAndParameters' ':' typeParameters = STRING';'
    	'methodCall' ':' methodCall = STRING';'
    	'methodParameters' ':' methodParameter = STRING';'
    	'languageElementName' ':' languageElementName = ID';'
    	'languageElement' ':' languageElement = STRING';'
    	
    	// constraints for root model elements
		(constraints += Constraint)*
    	createFacadeAction = CreateFacadeAction
    	('secondaryEffects' '{'
    		('create' 'effects' '{'
    			(createEffects += CreateEffect)*
    		'}')?
    		('delete' 'effects' '{'
    			(deleteEffects += DeleteEffect)*
    		'}')?
    	'}')?
    '}')?
;

DeleteAction:
    perspectiveActionType = PerspectiveActionType 'delete' 'action' name = ID (end ?= ';')? ('{'
    	(rootElement ?= 'rootElement'';')?
    	'roleName' ':' roleName = ID';'
    	'methodTypeAndParameters' ':' typeParameters = STRING';'
    	'methodCall' ':' methodCall = STRING';'
    	'methodParameters' ':' methodParameter = STRING';'
    	'languageElementName' ':' languageElementName = ID';'
    	'languageElement' ':' languageElement = STRING';'
    	
    	// constraints for root model elements
		(constraints += Constraint)*
    	deleteFacadeAction = DeleteFacadeAction
    	('secondaryEffects' '{'
    		('create' 'effects' '{'
    			(createEffects += CreateEffect)*
    		'}')?
    		('delete' 'effects' '{'
    			(deleteEffects += DeleteEffect)*
    		'}')?
    	'}')?
    '}')?
;

CreateFacadeAction:
    'facadeAction' 'create' name = ID '{'
    	'facade' 'calls' '{'
    		(facadeCalls += FacadeCall)*
    	'}'
    '}'
;

DeleteFacadeAction:
	'facadeAction' 'delete' name = ID '{'
		'facade' 'calls' '{'
			(facadeCalls += FacadeCall)+
		'}'
		
	'}'
;

OtherRootPackage:
	"otherRootPackage" otherRootPackage = STRING';'
;

ExplicitPackage:
	"explicitPackage" explicitPackage = STRING';'
;

//MethodCall:
//	'metaclassObject' ':' metaclassObject = STRING';'
//	'methodCall' ':' methodCall = STRING';'
//;

FacadeCall:
	'languageElement' ':' languageElement = STRING';'
	// constraints
	(constraints += Constraint)*
	(mappings += ParameterMapping)*
	'methodCall' ':' methodCall = STRING';'   
;


ParameterMapping:
	'derivedParameter' mapping = STRING';'
;

CreateEffect:
	'languageElement' ':' languageElement = STRING';'
	(mappings += ParameterMapping)*
	'methodCall' ':' methodCall = STRING';'
;

DeleteEffect:
	'element' ':' element = STRING';'
	'languageElement' ':' languageElement = STRING';'
	(mappings += ParameterMapping)*
	'methodCall' ':' methodCall = STRING';'
;

NavigationMapping:
	InterLanguageMapping | IntraLanguageMapping
	
;

InterLanguageMapping:
	'interLanguage' 'mapping' name = ID '{'
		(active ?= 'active')?
		(default ?= 'default')?
		'languageMapping' ':' languageMapping = [LanguageElementMapping]';'
		'interLanguage' 'mappingEnds' '{'
			(interLanguageMappingEnds += InterLanguageMappingEnd)*
		'}' 
	'}'
	
;

IntraLanguageMapping:
	'intraLanguage' 'mapping' name = ID '{'
		(active ?= 'active')?
		(closure ?= 'closure')?
		(reuse ?= 'reuse')?
		(increaseDepth ?= 'increaseDepth')?
		(changeModel ?= 'changeModel')?
		'from' ':' from = ID';'
		'hops' '{'
			(hop += Hop)*
		'}'
	'}'	
;

InterLanguageMappingEnd:
	'mapping' 'end' name = ID '{'
		(origin ?= 'origin')?
		(destination ?= 'destination')?
		'mappingEnd' ':' mappingEnd = [MappingEnd]';'
	'}'
;

Hop:
	'hop' ':' hop = ID';'
;

Constraint:
	'invariant' 'constraint' ':' definition = STRING';'
;



enum Cardinality:
    COMPULSORY = '1' | OPTIONAL='0..1' | COMPULSORY_MULTIPLE='1..*' | OPTIONAL_MULTIPLE='0..*'
;

enum PerspectiveActionType :
    REEXPOSE='re_expose' | REDEFINED='redefined' | CREATE_MAPPING='create_mapping'
;

enum LanguageActionType :
	CREATE = 'create' | DELETE = 'delete' | UPDATE = 'update'
;

enum BooleanType:
    TRUE = 'true' | FALSE = 'false' 
;




