grammar ca.mcgill.sel.perspectivedsl.ca.mcgill.sel.PerspectiveDSL with org.eclipse.xtext.common.Terminals

generate perspectiveDSL "http://www.mcgill.ca/sel/perspectivedsl/ca/mcgill/sel/PerspectiveDSL"

PerspectiveModel:
    (perspectives += Perspective)*
;


Perspective:
    'perspective' name = ID '{'
    
        ('default' default = ID';')?
        'display' 'name' displayName = STRING';'
        'namePrefix' namePrefix = ID';'
        
        ('model' 'factory' 'facade' 'calls' '{'
    		(modelFacades += FacadeCall)*
    	'}')?

        'languages' '{'
            (languages += Language)*
        '}'

        ('mappings' '{'
            (mappings += LanguageElementMapping)*
        '}')?

    '}'
;

Language:
    'existing' 'language' name = ID '{'
        "rootPackage" rootPackage = STRING';'
        "packageClassName" packageClassName = ID';'
        "controllerPackage" controllerPackage = STRING';'
        "roleName" roleName = ID';' 
        (otherRootPackages += OtherRootPackage)*
        (explicitPackages += ExplicitPackage)*
        ('actions' '{'
            (actions += PerspectiveAction)*
        '}')?
        
    '}'
;

LanguageElementMapping:
    'mapping' name = ID '{'
//        'relationship' ':' relationship = Relationship';'
		'fromIsRootElement' ':' fromIsRootElement = BooleanType';'
		'toIsRootElement' ':' toIsRootElement = BooleanType';'
        'fromCardinality' ':' fromCardinality = Cardinality';'
        'toCardinality' ':' toCardinality = Cardinality';'
        'fromElement' ':' fromElement = ID 'from' fromRoleName = ID 'fromGetElement' fromGetElement = STRING';'
        'toELement' ':' toElement = ID 'from' toRoleName = ID 'toGetElement' toGetElement = STRING';'

        ('nested' 'mappings' '{'
            (nestedMappings += NestedMapping)*
        '}')?
    '}'
;

NestedMapping:
    'nested' 'mapping' name = ID '{'
    	'matchMaker' ':' matchMaker = BooleanType';'
        'fromElement' ':' fromElementName = STRING 'from' fromRoleName = ID';'
        'toElement' ':' toElementName = STRING 'from' toRoleName = ID';'
    '}'
;

PerspectiveAction:
	CreateAction | DeleteAction
;


CreateAction:
    perspectiveActionType = PerspectiveActionType 'create' 'action' name = ID (end ?= ';')? ('{'
    	(rootElement ?= 'rootElement'';')?
    	'roleName' ':' roleName = ID';'
    	'methodTypeAndParameters' ':' typeParameters = STRING';'
    	'methodCall' ':' methodCall = STRING';'
    	'methodParameters' ':' methodParameter = STRING';'
    	'languageElementName' ':' languageElementName = ID';'
    	'languageElement' ':' languageElement = STRING';'
    	createFacadeAction = CreateFacadeAction
    	('secondaryEffects' '{'
    		('create' 'effects' '{'
    			(createEffects += CreateEffect)*
    		'}')?
    		('delete' 'effects' '{'
    			(deleteEffects += DeleteEffect)*
    		'}')?
    	'}')?
    '}')?
;

DeleteAction:
    perspectiveActionType = PerspectiveActionType 'delete' 'action' name = ID (end ?= ';')? ('{'
    	(rootElement ?= 'rootElement'';')?
    	'roleName' ':' roleName = ID';'
    	'methodTypeAndParameters' ':' typeParameters = STRING';'
    	'methodCall' ':' methodCall = STRING';'
    	'methodParameters' ':' methodParameter = STRING';'
    	'languageElementName' ':' languageElementName = ID';'
    	'languageElement' ':' languageElement = STRING';'
    	deleteFacadeAction = DeleteFacadeAction
    	('secondaryEffects' '{'
    		('create' 'effects' '{'
    			(createEffects += CreateEffect)*
    		'}')?
    		('delete' 'effects' '{'
    			(deleteEffects += DeleteEffect)*
    		'}')?
    	'}')?
    '}')?
;

CreateFacadeAction:
    'facadeAction' 'create' name = ID '{'
    	'facade' 'calls' '{'
    		(facadeCalls += FacadeCall)*
    	'}'
    '}'
;

DeleteFacadeAction:
	'facadeAction' 'delete' name = ID '{'
		'facade' 'calls' '{'
			(facadeCalls += FacadeCall)+
		'}'
		
	'}'
;

OtherRootPackage:
	"otherRootPackage" otherRootPackage = STRING';'
;

ExplicitPackage:
	"explicitPackage" explicitPackage = STRING';'
;

//MethodCall:
//	'metaclassObject' ':' metaclassObject = STRING';'
//	'methodCall' ':' methodCall = STRING';'
//;

FacadeCall:
	'languageElement' ':' languageElement = STRING';'
	(mappings += ParameterMapping)*
	'methodCall' ':' methodCall = STRING';'   
;


ParameterMapping:
	'derivedParameter' mapping = STRING';'
;

CreateEffect:
	'languageElement' ':' languageElement = STRING';'
	(mappings += ParameterMapping)*
	'methodCall' ':' methodCall = STRING';'
;

DeleteEffect:
	'element' ':' element = STRING';'
	'languageElement' ':' languageElement = STRING';'
	(mappings += ParameterMapping)*
	'methodCall' ':' methodCall = STRING';'
;



enum Cardinality:
    COMPULSORY = '1' | OPTIONAL='0..1' | COMPULSORY_MULTIPLE='1..*' | OPTIONAL_MULTIPLE='0..*'
;

enum PerspectiveActionType :
    REEXPOSE='re_expose' | REDEFINED='redefined' | CREATE_MAPPING='create_mapping'
;

enum LanguageActionType :
	CREATE = 'create' | DELETE = 'delete' | UPDATE = 'update'
;

enum BooleanType:
    TRUE = 'true' | FALSE = 'false' 
;